/* This file is processed by bison/yacc to generate cf_parser.[h/c]
   cf_parser.h is used by both the parser and lexer (generated by flex)
   Most notably the header includes strings and enums for the tokens.

   cf_parser.c is the parser logic, (the program which takes tokens from the
   lexer, validates the syntax and builds a data structure / syntax tree).

   See cf_tokenizer.l for the rules which generate the lexer (tokenizer).
*/

/* Added to the top of .c file: */
%code top {
#include <stdio.h>
#include <string.h>
}

/* Added to both .h and .c file (after top): */
%code requires {
#include "parse_lib.h"
}

/* Added to .c file (after top and requires): */
%code {

extern char *yylval;

int yylex(Parser *parser);

void yyerror(Parser *parser, const char *str)
{
    fprintf(stderr, "%s: '%s'\n", str, yylval);
    parser->errors += 1;
}

int yywrap()
{
    return 1;
}

}

%define api.value.type {char *}
%param {Parser *parser}
%token SEMICOLON COMMA OPEN_CURLY CLOSE_CURLY FAT_ARROW BODY QUOTED_STRING IDENTIFIER
%token-table

%%
policy_file:
    /* empty */
    | body_list
    ;

body_list:
    body
    | body_list body
    ;

body:
    BODY
    IDENTIFIER
    IDENTIFIER
    body_body
    ;

body_body:
    OPEN_CURLY
    body_inner_body
    CLOSE_CURLY
    ;

body_inner_body:
    /* empty */
    | body_attribute_list
    ;

body_attribute_list:
    body_attribute
    | body_attribute_list body_attribute
    ;

body_attribute:
    IDENTIFIER
    FAT_ARROW
    value
    SEMICOLON
    ;

value:
    IDENTIFIER
    | QUOTED_STRING
    | list
    ;

list:
    OPEN_CURLY
    inner_list
    CLOSE_CURLY
    ;

inner_list:
    /* empty */
    | non_empty_list;

non_empty_list:
    value
    | non_empty_list COMMA value
    ;
%%

void print_token(int t, const char *string) {
  printf("%d - %-16s - '%s'\n", t, yytname[YYTRANSLATE(t)], string);
}
